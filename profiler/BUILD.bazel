load(
    "//ign_bazel:cmake_configure_file.bzl",
    "cmake_configure_file",
)
load(
    "//ign_bazel:generate_include_header.bzl",
    "generate_include_header",
)
load(
    "//ign_bazel:ign_export_header.bzl",
    "ign_export_header",
)

public_headers_no_gen = [
    "include/ignition/common/Profiler.hh",
]

sources = [
    "src/Profiler.cc",
    "src/RemoteryProfilerImpl.cc",
]

# Configuration for UNIX
RMT_ENABLED = 1

RMT_USE_TINYCRT = 0

RMT_USE_CUDA = 0

RMT_USE_D3D11 = 0

RMT_USE_OPENGL = 1

RMT_USE_METAL = 0

cmake_configure_file(
    name = "remotery_config",
    src = "src/RemoteryConfig.h.in",
    out = "include/RemoteryConfig.h",
    cmakelists = ["CMakeLists.txt"],
    defines = [
        "RMT_ENABLED=%d" % RMT_ENABLED,
        "RMT_USE_TINYCRT=%d" % RMT_USE_TINYCRT,
        "RMT_USE_CUDA=%d" % RMT_USE_CUDA,
        "RMT_USE_D3D11=%d" % RMT_USE_D3D11,
        "RMT_USE_OPENGL=%d" % RMT_USE_OPENGL,
        "RMT_USE_METAL=%d" % RMT_USE_METAL,
    ],
    visibility = ["//visibility:private"],
)

ign_export_header(
    name = "include/ignition/common/profiler/Export.hh",
    export_base = "IGNITION_COMMON_PROFILER",
    lib_name = "ignition-common-profiler",
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "profiler_hh_genrule",
    out = "include/ignition/common/profiler.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/common/profiler/Export.hh",
        "include/RemoteryConfig.h",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/common/profiler/Export.hh",
    "include/ignition/common/profiler.hh",
    "src/ProfilerImpl.hh",
    "src/RemoteryProfilerImpl.hh",
    "include/RemoteryConfig.h",
]

cc_binary(
    name = "libignition-common3-profiler.so",
    srcs = sources + public_headers,
    defines = [
        "IGN_PROFILER_ENABLE=1",
        "IGN_PROFILER_REMOTERY=1",
    ],
    includes = ["include"],
    linkopts = ["-Wl,-soname,libignition-common3-profiler.so"],
    linkshared = True,
    visibility = [],
    deps = [
        "//ign_common",
        "//ign_common/profiler/src/Remotery:remotery",
    ],
)

cc_library(
    name = "profiler",
    srcs = ["libignition-common3-profiler.so"],
    hdrs = public_headers,
    includes = ["include"],
    linkstatic = True,
    visibility = ["//visibility:public"],
    deps = [":libignition-common3-profiler.so"],
    alwayslink = 1,
)

cc_test(
    name = "Profiler_Disabled_TEST",
    srcs = ["src/Profiler_Disabled_TEST.cc"],
    defines = [
        "IGN_PROFILER_ENABLE=0",
        "IGN_PROFILER_REMOTERY=0",
    ],
    deps = [
        "//ign_common",
        "//ign_common/profiler",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

cc_test(
    name = "Profiler_Remotery_TEST",
    srcs = ["src/Profiler_Remotery_TEST.cc"],
    deps = [
        "//ign_common",
        "//ign_common/profiler",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)
